
FIND_PACKAGE(Doxygen)
IF(DOXYGEN_FOUND)
    IF(LIBCHAOS_DOXYGEN)
        SET(DOXYGEN_AUTO ALL)
    ENDIF()

    # General Doxygen Options
    SET(DOXYGEN_INPUT ${CMAKE_SOURCE_DIR})
    SET(DOXYGEN_OUTPUT ${CMAKE_BINARY_DIR})
    SET(DOXYGEN_VERSION ${LIBCHAOS_DESCRIBE})
    IF(DEBUG)
        SET(DOXYGEN_DEBUG "YES")
    ELSE()
        SET(DOXYGEN_DEBUG "NO")
    ENDIF()
    SET(DOXYGEN_CONFIG_IN ${DOXYGEN_INPUT}/doc/Doxyfile.in)
    SET(DOXYGEN_CONFIG_OUT ${DOXYGEN_OUTPUT}/Doxyfile)
    SET(DOXYGEN_HTML ${DOXYGEN_OUTPUT}/html/index.html)
    SET(DOXYGEN_PDF ${DOXYGEN_OUTPUT}/${PROJECT_NAME}.pdf)

    # Build Compressed HTML on Windows
    INCLUDE(cmake/FindHTMLHelp.cmake)
    IF(HTML_HELP_COMPILER)
        SET(DOXYGEN_HHC ${HTML_HELP_COMPILER})
        CONFIGURE_FILE("${CMAKE_SOURCE_DIR}/doc/Doxyfile-help.in" "${CMAKE_BINARY_DIR}/Doxyfile-help" @ONLY)
        ADD_CUSTOM_TARGET(doxyhelp ${DOXYGEN_AUTO}
            DEPENDS ${LibChaos_ALL_FILES}
            WORKING_DIRECTORY ${CMAKE_BINARY_DIR}
            COMMAND ${DOXYGEN_EXECUTABLE} "${CMAKE_BINARY_DIR}/Doxyfile-help"
            COMMAND ${CMAKE_COMMAND} -E rename chm/LibChaos.chm LibChaos.chm
        )
    ENDIF()

    # Build Normal HTML
    CONFIGURE_FILE("${DOXYGEN_CONFIG_IN}" "${DOXYGEN_CONFIG_OUT}" @ONLY)
    ADD_CUSTOM_COMMAND(OUTPUT ${DOXYGEN_HTML}
        DEPENDS ${LibChaos_ALL_FILES} ${DOXYGEN_CONFIG_OUT}
        WORKING_DIRECTORY ${DOXYGEN_OUTPUT}
        COMMAND ${DOXYGEN_EXECUTABLE} "${DOXYGEN_CONFIG_OUT}"
    )

    # Build PDF
    ADD_CUSTOM_COMMAND(OUTPUT ${DOXYGEN_PDF}
        DEPENDS ${DOXYGEN_HTML}
        WORKING_DIRECTORY ${DOXYGEN_OUTPUT}/latex
        COMMAND make pdf
        COMMAND ${CMAKE_COMMAND} -E copy refman.pdf "${DOXYGEN_PDF}"
    )

    ADD_CUSTOM_TARGET(doxygen ${DOXYGEN_AUTO}
        DEPENDS ${DOXYGEN_HTML} ${DOXYGEN_PDF}
    )

ENDIF()
