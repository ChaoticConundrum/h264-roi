## Main LibChaos CMakeLists.txt
CMAKE_MINIMUM_REQUIRED(VERSION 2.8.12)

PROJECT(LibChaos)

# Options
OPTION(LIBCHAOS_ZPARCEL "Build ZParcel tool"                        OFF )

OPTION(LIBCHAOS_EXTERN  "Build LibChaos with External Libraries"    ON  )
OPTION(LIBCHAOS_UI      "Build LibChaos UI library"                 OFF )
OPTION(LIBCHAOS_TEST    "Build LibChaos Test suite"                 ON  )
OPTION(LIBCHAOS_UTIL    "Build LibChaos Utilities"                  OFF )

OPTION(LIBCHAOS_SHARED  "Build LibChaos as a shared library"        OFF )

OPTION(LIBCHAOS_DOXYGEN "Build LibChaos documentation with Doxygen" ON  )

# Build Settings
INCLUDE(cmake/ConfigureBuild.cmake)

# Git Describe
EXECUTE_PROCESS(
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
    COMMAND git describe --all --long --always --abbrev=8 --dirty=*
    OUTPUT_VARIABLE LIBCHAOS_DESCRIBE
    OUTPUT_STRIP_TRAILING_WHITESPACE
)
#SET(LIBCHAOS_CXX_FLAGS "${LIBCHAOS_CXX_FLAGS} -D\"_LIBCHAOS_DESCRIBE=${LIBCHAOS_DESCRIBE}\"")

IF(LIBCHAOS_LINK_SHARED)
    SET(LIBCHAOS_LINK SHARED)
    SET(LIBCHAOS_LINK_STR "Shared")
ELSE()
    SET(LIBCHAOS_LINK STATIC)
    SET(LIBCHAOS_LINK_STR "Static")
ENDIF()

# Export Options
GET_DIRECTORY_PROPERTY(HAS_PARENT PARENT_DIRECTORY)
IF(HAS_PARENT)
    SET(LibChaos_COMPILER   ${LIBCHAOS_COMPILER}    PARENT_SCOPE)
    SET(LibChaos_PLATFORM   ${LIBCHAOS_PLATFORM}    PARENT_SCOPE)
    SET(LibChaos_BUILD      ${LIBCHAOS_BUILD}       PARENT_SCOPE)

    SET(COMPILER_GCC        ${COMPILER_GCC}         PARENT_SCOPE)
    SET(COMPILER_MINGW      ${COMPILER_MINGW}       PARENT_SCOPE)
    SET(COMPILER_MSVC       ${COMPILER_MSVC}        PARENT_SCOPE)
    SET(COMPILER_CLANG      ${COMPILER_CLANG}       PARENT_SCOPE)

    SET(PLATFORM_LINUX      ${PLATFORM_LINUX}       PARENT_SCOPE)
    SET(PLATFORM_FREEBSD    ${PLATFORM_FREEBSD}     PARENT_SCOPE)
    SET(PLATFORM_WINDOWS    ${PLATFORM_WINDOWS}     PARENT_SCOPE)
    SET(PLATFORM_MACOSX     ${PLATFORM_MACOSX}      PARENT_SCOPE)

    SET(LibChaos_CXX_FLAGS  ${LIBCHAOS_CXX_FLAGS}   PARENT_SCOPE)
ENDIF()

SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${LIBCHAOS_CXX_FLAGS}")

# Readme Files
ADD_CUSTOM_TARGET(dummy SOURCES
    doc/README.txt
    doc/Projects.txt
    doc/LibChaos_Todo_Ideas.txt
)

## Libraries

# LibChaos
ADD_SUBDIRECTORY(chaos)
SET(LibChaos_BUILD_LIST "Core")
SET(LibChaos_INCLUDE ${LibChaos_Core_INCLUDE})
SET(LibChaos_ALL_FILES ${LibChaos_Core_FILES})

# LibChaos Extern
IF(NOT LIBCHAOS_EXTERN)
    SET(LIBCHAOS_EXTERN_ALL EXCLUDE_FROM_ALL)
ENDIF()
ADD_SUBDIRECTORY(extern)
SET(LibChaos_ALL_FILES ${LibChaos_ALL_FILES} ${LibChaos_Extern_FILES})
IF(LIBCHAOS_EXTERN)
    SET(LibChaos_BUILD_LIST ${LibChaos_BUILD_LIST} "Extern")
    SET(LibChaos_INCLUDE ${LibChaos_INCLUDE} ${LibChaos_Extern_INCLUDE})
ENDIF()

# Export Include Directories
IF(HAS_PARENT)
    SET(LibChaos_INCLUDE ${LibChaos_INCLUDE} PARENT_SCOPE)
ENDIF()

# LibChaos UI
IF(NOT LIBCHAOS_UI)
    SET(LIBCHAOS_UI_ALL EXCLUDE_FROM_ALL)
ENDIF()
ADD_SUBDIRECTORY(ui)
SET(LibChaos_ALL_FILES ${LibChaos_ALL_FILES} ${LibChaos_UI_FILES})
IF(LIBCHAOS_UI)
    SET(LibChaos_BUILD_LIST ${LibChaos_BUILD_LIST} "UI")
    IF(HAS_PARENT)
        SET(LibChaosUI_INCLUDE ${LibChaosUI_INCLUDE} PARENT_SCOPE)
    ENDIF()
ENDIF()

## Tools

# LibChaos Test
IF(NOT LIBCHAOS_TEST)
    SET(LIBCHAOS_TEST_ALL EXCLUDE_FROM_ALL)
ENDIF()
ADD_SUBDIRECTORY(test)
IF(LIBCHAOS_TEST)
    SET(LibChaos_BUILD_LIST ${LibChaos_BUILD_LIST} "Test")
ENDIF()

# LibChaos Utils
IF(NOT LIBCHAOS_UTIL)
    SET(LIBCHAOS_UTIL_ALL EXCLUDE_FROM_ALL)
ENDIF()
ADD_SUBDIRECTORY(util)
IF(LIBCHAOS_UTIL)
    SET(LibChaos_BUILD_LIST ${LibChaos_BUILD_LIST} "Util")
ENDIF()

# ZParcel
IF(NOT LIBCHAOS_ZPARCEL)
    SET(LIBCHAOS_ZPARCEL_ALL EXCLUDE_FROM_ALL)
ENDIF()
ADD_SUBDIRECTORY(zparcel)
IF(LIBCHAOS_ZPARCEL)
    SET(LibChaos_BUILD_LIST ${LibChaos_BUILD_LIST} "ZParcel")
ENDIF()

# Doxygen
INCLUDE(cmake/Doxygen.cmake)

# Build Status Line
STRING(REPLACE ";" "," LIBCHAOS_BUILD_LIST_STRING "${LibChaos_BUILD_LIST}")
MESSAGE(STATUS "LibChaos: ${CONFIGURE_BUILD_STRING} (${LIBCHAOS_LINK_STR}) - ${LIBCHAOS_BUILD_LIST_STRING} - ${LIBCHAOS_DESCRIBE}")
